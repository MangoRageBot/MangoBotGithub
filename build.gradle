import org.mangorage.mangobotgradle.util.GitVersion

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }

    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
        classpath 'org.mangorage:MangoBotGradle:b-5.0.+'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'MangoBotGradle'
apply plugin: 'maven-publish'

group = 'org.mangorage'
version = GitVersion.getGitVersion().getVersionAsString()

println("Version: " + version)

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.lavalink.dev/releases/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    installer('org.mangorage:installer:4.0.5')
    bot('org.mangorage:mangobot:11.0.23')

    plugin('org.mangorage:mangobotplugin:11.1.9')
    plugin('org.mangorage:mangobotwebsite:1.0.21')

    library('org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5')
    library('org.kohsuke:github-api:1.321')

}

configurations.all {
    resolutionStrategy {
        force 'com.google.code.gson:gson:2.11.+'
    }
}

test {
    useJUnitPlatform()
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mangobot_version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['mangobotgithub.plugin.json']) {
        expand replaceProperties + [project: project]
    }
}

MangoBotConfig {
    setJarTask(jar)
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifact jar

        pom {
            name = 'MangoBot Github Plugin'
            description = 'The Github Plugin for MangoBot'
            url = 'https://github.com/MangoRageBot/MangoBotGithub'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/MangoBotGithub/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
            withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.library.getDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.minecraftforge.net/"

            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}