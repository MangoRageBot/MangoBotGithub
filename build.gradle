import org.mangorage.mangobotgradle.util.GitVersion

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }
        maven {
            url = "https://maven.mangorage.org/releases/"
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
        classpath 'org.mangorage:MangoBotGradle:6.0.17'
    }
}

plugins {
    id 'org.gradlex.extra-java-module-info' version '1.12'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'MangoBotGradle'
apply plugin: 'maven-publish'

group = 'org.mangorage'
version = GitVersion.getGitVersion().getVersionAsString()

println("Version: " + version)

MangoBotConfig {
    setJarTask(jar)
    addRunConfig {
        it.setName("runDevBot")
        it.addArgs("--launchTarget", "mangobot")
        it.addArgs("--dev")
    }
}

extraJavaModuleInfo {
    setDeriveAutomaticModuleNamesFromFileNames(true)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor {
        languageVersion.set(JavaLanguageVersion.of(24))
    })
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = "https://maven.mangorage.org/releases"
    }
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.lavalink.dev/releases/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    installer('org.mangorage:installer:4.0.26')
    bootstrap("org.mangorage:mangobotbootstrap:1.0.78")

    plugin('org.mangorage:mangobot:12.0.83')
    plugin('org.mangorage:mangobotplugin:12.0.47')

    plugin('org.mangorage:mangobotwebsite:2.0.16')

    library("com.google.code.gson:gson:2.10.1")

    library('org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5')
    library('org.kohsuke:github-api:1.321')
}



test {
    useJUnitPlatform()
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mangobot_version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['mangobotgithub.plugin.json']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifact jar

        pom {
            name = 'MangoBot Github Plugin'
            description = 'The Github Plugin for MangoBot'
            url = 'https://github.com/MangoRageBot/MangoBotGithub'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/MangoBotGithub/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
            withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.library.getDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases/"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}